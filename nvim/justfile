default:
  @just --list --unsorted --justfile {{justfile()}}

clean:
  rm ~/.local/share/nvim/ ~/.local/state/nvim/ -rf

branch := if env("BRANCH", "") != "" { "{{' -b' +  env_var(BRANCH) }}"} else {""}
git-host := "https://github.com"
vim-plugin-dir := "pack/vendor"
vim-target := "start"

plugin-add repo name target=vim-target:
  git submodule add{{branch}} {{git-host / repo + ".git"}} {{vim-plugin-dir / target / name}}
  git commit -m "{{"nvim: added plugin " + name}}"

[confirm]
plugin-delete name target=vim-target:
  git submodule deinit {{vim-plugin-dir / target / name}}
  git rm {{vim-plugin-dir / target / name}}
  rm -rf {{".git/modules" / vim-plugin-dir / target / name}}
  git commit -m "{{"nvim: removed plugin " + name}}"
