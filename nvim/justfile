set fallback

default:
  @just --list --unsorted --justfile {{justfile()}}

## ----------------------------------------------------------------------------
## Utils
debug *args:
  nvim {{args}} --startuptime /tmp/vim-startuptime

## ----------------------------------------------------------------------------
## Plugin Handling

git-root       := `git rev-parse --show-toplevel`
branch         := if env("BRANCH", "") != "" { ' -b ' +  env_var("BRANCH") } else {""}
git-host       := "https://github.com"
vim-plugin-dir := "pack/plugins"
vim-target     := "opt"

plugin-add repo name target=vim-target:
  git submodule add{{branch}} {{git-host / repo + ".git"}} {{vim-plugin-dir / target / name}}
  git commit -m "{{"nvim: added plugin " + name}}"

[confirm]
plugin-delete name target=vim-target:
  git submodule deinit {{vim-plugin-dir / target / name}}
  git rm {{vim-plugin-dir / target / name}}
  rm -rf {{git-root / ".git/modules/nvim" / vim-plugin-dir / target / name}}
  git commit -m "{{"nvim: removed plugin " + name}}"


## ----------------------------------------------------------------------------
## Housekeeping

# clean .local nvim folders
clean:
  rm ~/.local/share/nvim/ ~/.local/state/nvim/ -rf
